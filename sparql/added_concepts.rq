PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dsv:     <http://purl.org/iso25964/DataSet/Versioning#>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sh:      <http://raw.github.com/jneubert/skos-history/master/skos-history.ttl/>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>

# identify concepts inserted with a certain version

SELECT  ?concept ?prefLabel
WHERE
  { 
    # use current version as default
    GRAPH <http://zbw.eu/stw/version>
      {
        ?versionset dsv:currentVersionRecord/dsv:isVersionRecordOf ?currrentVersion
      }
    BIND(?currentVersion AS ?version)

    GRAPH ?version
      { 
        # this assumes that only one delta to a prior version exists
        # (not multiple, overlapping deltas!)
        ?delta sh:deltaTo ?version .

        # identify insertions and deletions parts/graphs
        ?delta dcterms:hasPart ?insertions .
        ?insertions rdf:type sh:SchemeDeltaInsertions .
        ?delta dcterms:hasPart ?deletions .
        ?deletions rdf:type sh:SchemeDeltaDeletions

        # for each inserted concept, a (newly inserted) prefLabel must exist ...
        GRAPH ?insertions
          { ?concept skos:prefLabel ?prefLabel }

        # ... without any prefLabel deleted for this concept
        FILTER NOT EXISTS {GRAPH ?deletions
            { ?concept skos:prefLabel [] }
        }

        # restrict output to a certain language
        FILTER (lang(?prefLabel) = 'en')
      }
  }
ORDER BY ?concept

