prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix dsv: <https://raw.github.com/JohanDS/Dataset-versioning--for-KOS-data-sets-/master/DataSetVersioning.owl#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix dcterms: <http://purl.org/dc/terms/>

# build a iso-25964 version history set, according to
# https://github.com/JohanDS/Dataset-versioning--for-KOS-data-sets-, based on
# http://www.niso.org/apps/group_public/download.php/9627/Correspondence%20ISO25964-SKOSXL-MADS-2012-10-21.pdf

CONSTRUCT
  {
    ?versionHistorySet dsv:currentVersionRecord ?currentVersionRecord .
    ?versionRecord a dsv:VersionHistoryRecord ;
        dsv:isVersionRecordOf ?version ;
        dsv:hasVersionHistorySet ?versionHistorySet ;
        dc:identifier ?id ;
        dc:date ?date .
    ?version dsv:hasVersionRecord ?versionRecord .
  }
WHERE
  {
    # get info about all versions
    ?scheme dcterms:hasVersion ?version .
    GRAPH ?version
      { ?scheme owl:versionInfo ?id .
        ?scheme dcterms:issued ?date
      }

    # get name and graph of the current version
    ?scheme owl:versionInfo ?versionName .
    ?scheme dcterms:hasVersion ?currentVersion .
    GRAPH ?currentVersion
      { [] owl:versionInfo ?versionName }

    # build versionHistorySet uri
    #BIND (uri(concat(str(?scheme), '/version/set')) AS ?versionHistorySet)
    #BIND (uri(concat(str(?version), '/record')) AS ?versionRecord)
    #BIND (uri(concat(str(?currentVersion), '/record')) AS ?currentVersionRecord)
    BIND (uri(concat(str(?scheme), '/versionset')) AS ?versionHistorySet)
    BIND (uri(replace(str(?version), '/version/', '/versionrecord/')) AS ?versionRecord)
    BIND (uri(replace(str(?currentVersion), '/version/', '/versionrecord/')) AS ?currentVersionRecord)
  }
