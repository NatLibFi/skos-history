PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dsv: <http://purl.org/iso25964/DataSet/Versioning#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>
PREFIX sh: <http://purl.org/skos-history/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xhv: <http://www.w3.org/1999/xhtml/vocab#>
#
# identify concepts deprecated with a certain version
#
SELECT (?concept AS ?deprecatedConcept) (str(?label) AS ?deprecatedConceptLabel)
(?replacedBy as ?replacedByConcept) (str(?replacedByPrefLabel) AS ?replacedByConceptLabel)
WHERE {
  # parameters
  VALUES ( ?versionHistoryGraph ?language ) {
    ( <http://zbw.eu/stw/version> "en" )
  }
  GRAPH ?versionHistoryGraph {
    # use the current version as default
    ?versionset dsv:currentVersionRecord ?currentVHR .
    ?currentVHR sh:usingNamedGraph/sd:name ?versionGraph .
    ?currentVHR sh:hasDelta ?delta .
    # get the delta to the previous version
    ?currentVHR xhv:prev ?previousVHR .
    ?delta sh:deltaTo ?currentVHR .
    ?delta sh:deltaFrom ?previousVHR .
    # identify insertions and deletions parts/graphs
    ?delta dcterms:hasPart ?insertions .
    ?insertions a sh:SchemeDeltaInsertions .
    ?insertions sh:usingNamedGraph/sd:name ?insertionsGraph .
    ?delta dcterms:hasPart ?deletions .
    ?deletions a sh:SchemeDeltaDeletions .
    ?deletions sh:usingNamedGraph/sd:name ?deletionsGraph .
  }
  # identify the deprecated concepts
  GRAPH ?insertionsGraph {
    ?concept owl:deprecated true
  }
  GRAPH ?versionGraph {
    # different kinds of remaining labels should be supported
    {
      ?concept skos:prefLabel ?label
    }
    UNION
    {
      ?concept rdfs:label ?label
    }
    # perhaps the deprecated concept was merged into another one
    OPTIONAL {
      ?concept dcterms:isReplacedBy ?replacedBy .
      ?replacedBy skos:prefLabel ?replacedByPrefLabel .
      FILTER (lang(?label) = lang(?replacedByPrefLabel))
    }
  }
  # restrict output to a certain language
  FILTER (lang(?label) = ?language)
}
ORDER BY ?concept
