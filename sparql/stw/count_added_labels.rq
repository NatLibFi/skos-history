PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dsv: <http://purl.org/iso25964/DataSet/Versioning#>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>
PREFIX sh: <http://purl.org/skos-history/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xhv: <http://www.w3.org/1999/xhtml/vocab#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
# Count added labels in a certain language per version
#
SELECT ?version (str(count(distinct ?label)) AS ?addedLabelCount)
WHERE {
  # parameters
  VALUES ( ?versionHistoryGraph ?conceptType ?language ) {
    ( <http://zbw.eu/stw/version> zbwext:Descriptor "en" )
  }
  GRAPH ?versionHistoryGraph {
    ?delta a sh:SchemeDelta .
    ?delta sh:deltaTo/dc:identifier ?version .
    ?delta sh:deltaTo/sh:usingNamedGraph/sd:name ?versionGraph .
    # identify insertions and deletions parts/graphs
    ?delta dcterms:hasPart ?insertions .
    ?insertions a sh:SchemeDeltaInsertions .
    ?insertions sh:usingNamedGraph/sd:name ?insertionsGraph .
    ?delta dcterms:hasPart ?deletions .
    ?deletions a sh:SchemeDeltaDeletions .
    ?deletions sh:usingNamedGraph/sd:name ?deletionsGraph .
  }
  # for each inserted label ...
  GRAPH ?insertionsGraph {
    ?concept skos:prefLabel | skos:altLabel ?label
  }
  # ... which was not attached to some (other) concept before
  FILTER NOT EXISTS {
    GRAPH ?deletionsGraph {
      [] skos:prefLabel | skos:altLabel ?label
    }
  }
  # filter on concept type
  GRAPH ?versionGraph {
    ?concept a ?conceptType .
  }
  FILTER (lang(?label) = ?language)
}
GROUP BY ?version
ORDER BY ?version
