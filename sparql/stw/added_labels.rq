PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dsv: <http://purl.org/iso25964/DataSet/Versioning#>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>
PREFIX sh: <http://purl.org/skos-history/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xhv: <http://www.w3.org/1999/xhtml/vocab#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
# Show all labels added in the current version
#
SELECT  (str(?label) AS ?addedLabel) ?concept (str(?prefLabel) as ?conceptLabel)
WHERE {
  # parameters
  VALUES ( ?versionHistoryGraph ?conceptType ?language ) {
    ( <http://zbw.eu/stw/version> zbwext:Descriptor "en" )
  }
  GRAPH ?versionHistoryGraph {
    # the compared versions default to the current and the previous one
    ?versionset dsv:currentVersionRecord/xhv:prev/dc:identifier ?oldVersion .
    ?versionset dsv:currentVersionRecord/dc:identifier ?newVersion .
    # get the delta and via that the relevant graphs
    ?delta a sh:SchemeDelta ;
      sh:deltaFrom/dc:identifier ?oldVersion ;
      sh:deltaTo/dc:identifier ?newVersion ;
      sh:deltaTo/sh:usingNamedGraph/sd:name ?newVersionGraph ;
      dcterms:hasPart ?insertions ;
      dcterms:hasPart ?deletions .
    ?insertions a sh:SchemeDeltaInsertions ;
      sh:usingNamedGraph/sd:name ?insertionsGraph .
    ?deletions a sh:SchemeDeltaDeletions ;
      sh:usingNamedGraph/sd:name ?deletionsGraph .
  }
  # get all labels
  GRAPH ?insertionsGraph {
    [] skos:prefLabel | skos:altLabel ?label
  }
  # ... which were not attached to some (other) concept before
  FILTER NOT EXISTS {
    GRAPH ?deletionsGraph {
      [] skos:prefLabel | skos:altLabel ?label
    }
  }
  FILTER (lang(?label) = ?language)
  GRAPH ?newVersionGraph {
    # get concepts and restrict to descriptors
    ?concept skos:prefLabel | skos:altLabel ?label ;
      a ?conceptType ;
      skos:prefLabel ?prefLabel .
  }
  # restrict output to a certain language
  FILTER (lang(?prefLabel) = ?language)
}
ORDER BY ?label
