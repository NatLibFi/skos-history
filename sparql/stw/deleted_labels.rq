PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dsv: <http://purl.org/iso25964/DataSet/Versioning#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX sd: <http://www.w3.org/ns/sparql-service-description#>
PREFIX sh: <http://purl.org/skos-history/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX xhv: <http://www.w3.org/1999/xhtml/vocab#>
PREFIX zbwext: <http://zbw.eu/namespaces/zbw-extensions/>
#
# show all labels deleted in the current version
#
SELECT  (str(?label) AS ?deletedLabel) ?previousConcept (str(?prefLabel) AS ?previousConceptLabel)
WHERE {
  # parameters
  VALUES ( ?versionHistoryGraph ?type ?language ) {
    ( <http://zbw.eu/stw/version> "Descriptor" "de" )
  }
  GRAPH ?versionHistoryGraph {
    # use the current version as default
    ?versionset dsv:currentVersionRecord ?currentVHR .
    ?currentVHR sh:hasDelta ?delta .
    ?currentVHR sh:usingNamedGraph/sd:name ?versionGraph .
    # get the delta to the previous version
    ?currentVHR xhv:prev ?previousVHR .
    ?previousVHR sh:usingNamedGraph/sd:name ?previousVersionGraph .
    ?delta sh:deltaTo ?currentVHR .
    ?delta sh:deltaFrom ?previousVHR .
    # identify insertions and deletions parts/graphs
    ?delta dcterms:hasPart ?insertions .
    ?insertions a sh:SchemeDeltaInsertions .
    ?insertions sh:usingNamedGraph/sd:name ?insertionsGraph .
    ?delta dcterms:hasPart ?deletions .
    ?deletions a sh:SchemeDeltaDeletions .
    ?deletions sh:usingNamedGraph/sd:name ?deletionsGraph .
  }
  # get all deleted labels
  GRAPH ?deletionsGraph {
    [] skos:prefLabel | skos:altLabel ?label 
  }
  # ... which were not attached to some (other) concept now
  FILTER NOT EXISTS {
    GRAPH ?insertionsGraph {
      [] skos:prefLabel | skos:altLabel ?label 
    }
  }
  GRAPH ?previousVersionGraph {
    # get concepts and restrict to descriptors
    ?previousConcept skos:prefLabel | skos:altLabel ?label .
    ?previousConcept skos:prefLabel ?prefLabel .
    # STW 8.04 used a different custom namespace URI
    BIND (uri(concat("http://zbw.eu/namespaces/skos-extensions/", ?type)) as ?oldConceptType)
    BIND (uri(concat("http://zbw.eu/namespaces/zbw-extensions/", ?type)) as ?conceptType)
    {
      ?previousConcept a ?oldConceptType 
    } UNION {
      ?previousConcept a ?conceptType
    }
  }
  # restrict output to a certain language
  FILTER (lang(?label) = ?language)
  FILTER (lang(?prefLabel) = ?language)
}
ORDER BY ?label
