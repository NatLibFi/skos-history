PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX owl:     <http://www.w3.org/2002/07/owl#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX sh:      <http://raw.github.com/jneubert/skos-history/master/skos-history.ttl/>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX zbwext:  <http://zbw.eu/namespaces/zbw-extensions/>

# show all labels added in the last version

SELECT  ?label ?concept
WHERE
  { 
    # fixed version, will be parameter or current version
    BIND(<http://zbw.eu/stw/version/8.12> AS ?version)

    GRAPH ?version
      { 
        # this assumes that only one delta to a prior version exists
        # (not multiple, overlapping deltas!)
        ?delta sh:deltaTo ?version .

        # identify insertions and deletions parts/graphs
        ?delta dcterms:hasPart ?insertions .
        ?insertions rdf:type sh:SchemeDeltaInsertions .
        ?delta dcterms:hasPart ?deletions .
        ?deletions rdf:type sh:SchemeDeltaDeletions

        # get all labels
        GRAPH ?insertions
          { [] skos:prefLabel | skos:altLabel ?label }

        # ... which were not attached to some (other) concept before
        FILTER NOT EXISTS {GRAPH ?deletions
            { [] skos:prefLabel | skos:altLabel ?label }
        }

        # get concepts and restrict to descriptors
        ?concept skos:prefLabel | skos:altLabel ?label .
        ?concept a zbwext:Descriptor .

        # restrict output to a certain language
        FILTER (lang(?label) = 'en')
      }
  }
ORDER BY ?label

